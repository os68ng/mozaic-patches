// Monologue Controller
//
// This controller allows you to maintain a fixed configuration on the external controller (like Beatstep Pro or any other).
// Furthermore you have a control panel in Mozaic where you can see and modify all values in one place.
// You can also use this template without an external controller.
//
// Author: os68ng
// Version: 1.0
// Licence: CC-BY
//

@Description
Monologue Controller

A simple controller frontend for Monologue. Also supports external controllers like BeatStep (Pro) for easy mapping and visualization of the values.

SHIFT will send all knob values to the device (some sort of a simple preset manager).
@End

@Config
  midichdetect = 0
@End

@OnLoad
  ShowLayout 1
  SetShortName {Monologue}
  Call @Init
@End

@OnShiftDown
  Call @SendAllKnobs
@End

@OnMidiInput
  if MIDIChannel = midich
    if MIDICommand = 0xB0 // CC
      Call @HandleController
    else
      SendMIDIThruOnCh MIDIChannel
    endif
  endif
@End

@OnKnobChange
  Call @HandleKnobChange
@End

// ========================================

@Init
  Call @Config
  if Unassigned midich
    midich = 0
  endif

  Call @InitUI
  Call @InitKnobCCMapping
  Call @InitCCKnobMapping
@End

@InitUI
  LabelKnobs {Monologue Controller}
  LabelKnob 0, {VCO1 Wave}
  LabelKnob 1, {VCO1 Shp}
  LabelKnob 2, {VCO1 Level}
  LabelKnob 3, {VCO2 Wave}
  LabelKnob 4, {VCO2 Oct}
  LabelKnob 5, {VCO2 Shp}
  LabelKnob 6, {VCO2 Level}
  LabelKnob 7, {Sync/Ring}
  LabelKnob 8, { }
  LabelKnob 9, { }
  LabelKnob 10, { }
  LabelKnob 11, {VCF Cutoff}
  LabelKnob 12, {VCF Reso}
  LabelKnob 13, {LFO Rate}
  LabelKnob 14, {LFO Depth}
  LabelKnob 15, {EG Attack}
  LabelKnob 16, {EG Decay}
  LabelKnob 17, {EG Int}
  LabelKnob 18, {Drive}
  LabelKnob 19, { }
  LabelKnob 20, { }

  SetKnobValue 21,midich*8
  LabelKnob 21, {Chan }, midich+1

@End

@InitKnobCCMapping
  FillArray ccmap,0,22
  ccmap[0] = 50   // VCO1 Wave
  ccmap[1] = 36   // VCO1 Shape
  ccmap[2] = 39   // VCO1 Level
  ccmap[3] = 51   // VCO2 Wave
  ccmap[4] = 49   // VCO2 Octave
  ccmap[5] = 37   // VCO2 Shape
  ccmap[6] = 40   // VCO2 Level
  ccmap[7] = 61   // Sync/Ring
  ccmap[11] = 43  // VCF Cutoff
  ccmap[12] = 44  // VCF Resonance
  ccmap[13] = 24  // LFO Rate
  ccmap[14] = 26  // LFO Depth
  ccmap[15] = 16  // EG Attack
  ccmap[16] = 17  // EG Decay
  ccmap[17] = 25  // EG Int
  ccmap[18] = 28  // Drive
@End

@InitCCKnobMapping
  FillArray ccknob,-1,128
  ccknob[16] = 0
  ccknob[17] = 1
  ccknob[18] = 2
  ccknob[19] = 3
  ccknob[20] = 4
  ccknob[21] = 5
  ccknob[22] = 6
  ccknob[23] = 7
  ccknob[24] = 11
  ccknob[25] = 12
  ccknob[26] = 13
  ccknob[27] = 14
  ccknob[28] = 15
  ccknob[29] = 16
  ccknob[30] = 17
  ccknob[31] = 18
@End

// ========================================
// from lib
// ========================================

@HandleController
  Call @HandleMidiChannelDetection
  kidx = ccknob[MIDIByte2]
  if kidx >= 0
    ctrl = ccmap[kidx]
    cval = MIDIByte3
    SetKnobValue kidx,cval
    Call @SendCC
  endif
@End

@HandleMidiChannelDetection
  if midichdetect = 1 and midich <> MIDIChannel
    midich = MIDIChannel
    SetKnobValue 21,midich*8
    LabelKnob 21, {Chan }, midich+1
  endif
@End

@HandleKnobChange
  ctrl = 0
  cval = GetKnobValue LastKnob
  if LastKnob = 21
    midich = Div cval, 8
    LabelKnob 21, {Chan }, midich+1
  else
    ctrl = ccmap[LastKnob]
  endif
  Call @SendCC
@End

@SendAllKnobs
  for i = 0 to 20
    ctrl = ccmap[i]
    cval = GetKnobValue i
    Call @SendCC
  endfor
@End

@SendCC
  if ctrl <> 0
    SendMIDICC midich, ctrl, cval
  endif
@End
